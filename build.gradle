plugins {
    id "java"
    id "checkstyle"
    id "jacoco"
    id "maven"
    id 'maven-publish'
    id "org.embulk.embulk-plugins" version "0.4.2"
}

repositories {
    mavenCentral()
    jcenter()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "com.treasuredata.embulk.plugins"
description = "An Embulk plugin to loads records from Azure Blob Storage."
version = "0.3.1"

def embulkVersion = '0.10.28'

dependencies {
    compileOnly "org.embulk:embulk-api:$embulkVersion"
    compileOnly "org.embulk:embulk-spi:$embulkVersion"
    compile("org.embulk:embulk-util-config:0.3.0") {
        // Conflict with Embulk Core
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'javax.validation', module: 'validation-api'
    }

    compile('org.embulk:embulk-util-retryhelper-jetty92:0.8.1') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    compile 'org.embulk:embulk-util-file:0.1.3'

    // Explicit dependencies for embulk-util-* & embulk-base-restclient that matches with Embulk 0.10.19
    compile 'com.fasterxml.jackson.core:jackson-core:2.6.7'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.6.7'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.7'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7'
    compile 'javax.validation:validation-api:1.1.0.Final'

    compile 'com.google.guava:guava:18.0'
    compile 'org.apache.commons:commons-lang3:3.4'

    compile fileTree(dir: 'libs', include: '*.jar')

    compile "com.microsoft.azure:azure-storage:8.0.0"

    testCompile "junit:junit:4.12"
    testCompile "org.embulk:embulk-core:$embulkVersion"
    testCompile "org.embulk:embulk-core:$embulkVersion:tests"
    testCompile "org.embulk:embulk-deps:$embulkVersion"
    testCompile "org.embulk:embulk-standards:$embulkVersion"
}

test {
    jvmArgs '-XX:MaxPermSize=128M', '-Xmx2048m'
    testLogging {
        events "passed", "skipped", "failed"
    }
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
}

embulkPlugin {
    mainClass = "org.embulk.input.azure_blob_storage.AzureBlobStorageInputPlugin"
    category = "input"
    type = "azure_blob_storage"
}

gem {
    authors = [ "Satoshi Akama" ]
    email = [ "satoshiakama@gmail.com" ]
    summary = "Azure Blob Storage input plugin for Embulk"
    homepage = "https://github.com/embulk/embulk-input-azure_blob_storage"
    licenses = [ "Apache-2.0" ]
}
